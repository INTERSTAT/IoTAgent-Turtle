// Following https://www.w3.org/TR/turtle/#sec-grammar-grammar

start: statement*
statement: directive | triples "."
directive: prefixid
prefixid: "@prefix" prefixname_ns iri "."
prefixname_ns: PREFIXNAME_PREFIX? ":"
triples: subject predicateobjectlist
subject: iri
iri: URIREF | prefixedname
predicateobjectlist: verb objectlist
objectlist: object ("," object)*
object: iri
verb: "a"
prefixedname: PNAME_LN | PNAME_NS

// Tokens
PREFIXNAME_PREFIX: PREFIXNAME_CHARS_BASE ((PREFIXNAME_CHARS | ".")* PREFIXNAME_CHARS)?
PREFIXNAME_CHARS_BASE: "A".."Z" | "a".."z"
PREFIXNAME_CHARS: PREFIXNAME_CHARS_U | "-" | "0".."9"
PREFIXNAME_CHARS_U: PREFIXNAME_CHARS_BASE | "_"
PNAME_LN: PNAME_NS PN_LOCAL
PNAME_NS: PREFIXNAME_PREFIX? ":"
PN_LOCAL: (PREFIXNAME_CHARS_U | ":" | "0".."9" | PLX) ((PREFIXNAME_CHARS | "." | ":" | PLX)* (PREFIXNAME_CHARS | ":" | PLX))?
PLX: PERCENT
PERCENT: "%" HEX HEX
HEX: "0".."9" | "A".."F" | "a".."f"
URIREF: "<" /[^<>{}]+/ ">"
COMMENT: /#[^\n]*/


%import common.WS -> WS

// Disregard spaces in text and comments
%ignore WS
%ignore COMMENT
